plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.5'
}
defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'uk.jamierocks.histacom'
archivesBaseName = 'HistacomCore'
version = '1.0.0-SNAPSHOT'
ext.url = 'http://histacom.jamierocks.uk/'

repositories {
    mavenCentral()
    maven {
        url 'http://repo.lexteam.xyz/maven/releases/'
    }
    maven {
        url 'http://repo.lexteam.xyz/maven/snapshots/'
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.code.gson:gson:2.5'
    compile 'xyz.lexteam:eventbus:1.3'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from 'LICENSE.txt'
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/Histacom/HistacomCore"
                        connection = "scm:git:https://github.com/Histacom/HistacomCore.git"
                        developerConnection = "scm:git:git@github.com:Histacom/HistacomCore.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/Histacom/HistacomCore/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
